/**
 * This file is part of INU Club.
 *
 * Copyright (C) 2021 INU Global App Center <potados99@gmail.com>
 *
 * INU Club is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * INU Club is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

// THE VERSION NAME!
ext.base = '0.0.1'
ext.phase = 'alpha6'

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:4.1.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.21"
        classpath 'com.google.gms:google-services:4.3.4'
    }
}

allprojects {
    ext {
        versionCode = getRevisionCount()
        versionName = getVersionName(base, phase)

        compileSdkVersion = 29
        targetSdkVersion = 29
        minSdkVersion = 26
    }

    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

/**
 * We will use the git revision count as a version code.
 * @return the git revision count, integer.
 */
static def getRevisionCount() {
    return Integer.parseInt("git rev-list --count HEAD".execute().text.trim())
}

static def getVersionName(base, phase) {
    String build = System.getenv("BITRISE_BUILD_NUMBER")

    String all = ""

    if (base)
        all += base

    if (build)
        all += ".$build"

    if (phase)
        all += "-$phase"

    return all
}